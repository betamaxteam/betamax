apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'org.ajoberstar.github-pages'

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
		classpath 'org.ajoberstar:gradle-git:1.2.0-milestone.1'
	}
}

version = "1.1.2-banno-SNAPSHOT"
group = 'co.freeside'
archivesBaseName = 'betamax'

repositories {
	mavenCentral()
}

nexus {
  repositoryUrl  "http://nexus.banno.com/nexus/content/repositories/releases/"
  snapshotRepositoryUrl "http://nexus.banno.com/nexus/content/repositories/snapshots/"

}

dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.3.0:indy',
        'junit:junit:4.10',
        'org.yaml:snakeyaml:1.13',
        'org.apache.httpcomponents:httpclient:4.3.6',
        'commons-lang:commons-lang:2.6',
        'org.eclipse.jetty:jetty-server:8.1.2.v20120308'

	testCompile('org.codehaus.groovy.modules.http-builder:http-builder:0.7.1') {
		exclude module: 'groovy'
		exclude module: 'httpclient'
	}
	testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
		exclude module: 'groovy-all'
		exclude module: 'junit-dep'
	}
	testCompile 'commons-httpclient:commons-httpclient:3.1',
				'com.github.groovy-wslite:groovy-wslite:0.7.1'

	testRuntime 'cglib:cglib-nodep:3.1'
}

modifyPom {
	dependencies.removeAll(dependencies.findAll { it.scope == 'test' })

	project {
		name 'Betamax - An HTTP stubbing proxy for testing JVM applications.'
		packaging 'jar'
		description 'Betamax is a Groovy record/playback proxy for stubbing access to external HTTP resources when testing. Inspired by Ruby\'s VCR.'
		url 'http://freeside.co/betamax'
		inceptionYear '2011'

		scm {
			url 'scm:git@github.com:robfletcher/betamax.git'
			connection 'scm:git@github.com:robfletcher/betamax.git'
			developerConnection 'scm:git@github.com:robfletcher/betamax.git'
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id 'robfletcher'
				name 'Rob Fletcher'
				url 'http://blog.freeside.co/'
				roles {
					role 'Lead'
					role 'Founder'
				}
			}
		}
	}
}

nexus {
	sign = true
}

codenarc {
	toolVersion = '0.17'
	ignoreFailures = true
}

githubPages {
	repoUri = 'git@github.com:robfletcher/betamax.git'
	workingPath = "$buildDir/docs"
	pages {
		exclude '**/_site/**'
		from('src/docs') {
			exclude '**/*.less'
		}
	}
}

test {
	testLogging {
		quiet {
			events 'failed'
			exceptionFormat 'short'
		}
	}
}


