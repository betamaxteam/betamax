apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'nexus'
apply plugin: 'github-pages'

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
		classpath 'org.ajoberstar:gradle-git:0.2.0'
	}
}

version = '1.1-SNAPSHOT'
group = 'co.freeside'
archivesBaseName = 'betamax'

repositories {
	mavenCentral()
	mavenRepo url: 'http://oss.sonatype.org/content/groups/public/'
}

dependencies {
	groovy 'org.codehaus.groovy:groovy-all:1.7.8'

	compile 'junit:junit:4.8.2'
	compile 'org.apache.httpcomponents:httpclient:4.2.1'
	compile('org.eclipse.jetty:jetty-server:7.6.7.v20120910') {
		exclude module: 'javax.servlet'
	}
	compile 'org.eclipse.jetty.orbit:javax.servlet:2.5.0.v201103041518@jar'
	compile 'org.yaml:snakeyaml:1.11-SNAPSHOT'
	compile 'commons-lang:commons-lang:2.4'

	testCompile('org.codehaus.groovy.modules.http-builder:http-builder:0.5.2') {
		exclude module: 'groovy'
		exclude module: 'httpclient'
	}
	testCompile 'commons-httpclient:commons-httpclient:3.1'
	testCompile('org.spockframework:spock-core:0.6-groovy-1.7') {
		exclude module: 'groovy-all'
	}

	testRuntime 'cglib:cglib-nodep:2.2.2'
}

modifyPom {
	dependencies.removeAll(dependencies.findAll { it.scope == "test" })

	project {
		name 'Betamax - An HTTP stubbing proxy for testing JVM applications.'
		packaging 'jar'
		description 'Betamax is a Groovy record/playback proxy for stubbing access to external HTTP resources when testing. Inspired by Ruby\'s VCR.'
		url 'http://freeside.co/betamax'
		inceptionYear '2011'

		scm {
			url 'scm:git@github.com:robfletcher/betamax.git'
			connection 'scm:git@github.com:robfletcher/betamax.git'
			developerConnection 'scm:git@github.com:robfletcher/betamax.git'
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id 'robfletcher'
				name 'Rob Fletcher'
				url 'http://blog.freeside.co/'
				roles {
					role 'Lead'
					role 'Founder'
				}
			}
		}
	}
}

nexus {
	sign = true
}

codenarc {
	ignoreFailures = true
}

githubPages {
	repoUri = 'git@github.com:robfletcher/betamax.git'
	workingPath = "$buildDir/docs"
	pages {
		from 'src/docs'
	}
}
